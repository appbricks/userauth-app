buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.10.RELEASE")
        classpath("net.saliman:gradle-cobertura-plugin:2.2.4")
    }
}

apply plugin: "war"
apply plugin: "maven"
apply plugin: 'spring-boot'
apply plugin: "cobertura"
apply plugin: "eclipse"
apply plugin: "idea"

group = 'org.appbricks'
version =  "0.1.0-SNAPSHOT"

ext {
    springSocialGoogleVersion = '1.0.0.RELEASE'
    springSocialLinkedInVersion = '1.0.0.RELEASE'
    springSocialFacebookVersion = '2.0.3.RELEASE'
    springSocialTwitterVersion = '1.1.2.RELEASE'
    thymeleafExtrasSpringSecurityVersion = '2.1.1.RELEASE'
}

war {
    baseName = "ab-userauth-app"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/libs-release" }
}

dependencies {

    compile(
        ["org.springframework.boot:spring-boot-starter-thymeleaf"],
        ["org.springframework.boot:spring-boot-starter-security"],
        ["org.springframework.social:spring-social-security"],
        ["org.springframework.social:spring-social-google:${springSocialGoogleVersion}"],
        ["org.springframework.social:spring-social-linkedin:${springSocialLinkedInVersion}"],
        ["org.springframework.social:spring-social-facebook:${springSocialFacebookVersion}"],
        ["org.springframework.social:spring-social-twitter:${springSocialTwitterVersion}"],
        ["org.thymeleaf.extras:thymeleaf-extras-springsecurity3:${thymeleafExtrasSpringSecurityVersion}"],
        ["javax.servlet:jstl:1.2"],
        ["org.appbricks:ab-model-user:0.1.0-SNAPSHOT"],
        ["org.appbricks:ab-model-application:0.1.0-SNAPSHOT"]
    )
    compile('org.springframework:spring-orm:4.1.4.RELEASE') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    compile('org.springframework.security:spring-security-taglibs:3.2.5.RELEASE')

    providedRuntime(
        ["org.springframework.boot:spring-boot-starter-tomcat"],
        ["org.apache.tomcat.embed:tomcat-embed-jasper"]
    )

    compile('log4j:log4j:1.2.15') {
        exclude group: 'javax.mail', module: 'mail'
        exclude group: 'javax.jms', module: 'jms'
        exclude group: 'com.sun.jdmk', module: 'jmxtools'
        exclude group: 'com.sun.jmx', module: 'jmxri'
    }

    testCompile(
        ["org.testng:testng:6.8.21"],
        ["org.uncommons:reportng:1.1.4"],
        ["org.mockito:mockito-all:1.9.5"],
        ["org.easytesting:fest-assert-core:2.0M10"],
        // workaround for a known issue with TestNG 6.x: explicitly add Guice (Gradle will fail to run tests otherwise)
        ["com.google.inject:guice:3.0"]
    )
}

// ensure that test resources (src/test/resources) are added to CLASSPATH;
// see http://forums.gradle.org/gradle/topics/tests_arent_executed_when_setting_the_test_runtimeclasspath and
// http://gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html#org.gradle.api.tasks.testing.Test:classpath
sourceSets {
    main {
        runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
    }
    test {
        runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
    }
}

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: "gradle-testng-mockito-bootstrap", parallel: "tests") {
            test (name : "all-tests") {
                packages {
                    "package" (name: "org.appbricks.*")
                }
            }
        }
    }
    options {
        listeners << "org.uncommons.reportng.HTMLReporter"
        listeners << "org.uncommons.reportng.JUnitXMLReporter"
    }
    ext.useDefaultListeners = true
    ext.workingDirectory = "build/"
}

task startMongo(type:Exec) {
    executable "${project.projectDir}/data/start_mongo.sh"
}
task stopMongo(type:Exec) {
    executable "${project.projectDir}/data/stop_mongo.sh"
}

test.dependsOn startMongo
test.finalizedBy stopMongo
bootRun.dependsOn startMongo

// https://github.com/stevesaliman/gradle-cobertura-plugin
// http://ingorichter.blogspot.ch/2013/03/using-cobertura-with-gradle.html for list of options
cobertura {
    coverageFormats = ["xml", "html"]
}

// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    gradleVersion = "2.2.1"
}
